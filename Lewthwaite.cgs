/*
 * Lewthwaite.cgs
 *
 * Lewhtwaite's game is a partisan taquin.
 */

// Define Lewthwaite as a subclass of GridRuleset
singleton class Lewthwaite extends game.grid.GridRuleset

  // Define a nested class to represent specific positions
  class Position(grid as Grid) extends GridGame

    // Define the options of this position
    override def Options(player as Player) begin

      var us := player.Ordinal;

      for coord in (grid FindAll us)              // For each coordinate that matches the player's color ...
      for direction in Coordinates.Orthogonal     // ... and for each orthogonal direction ...
      where grid[coord + direction] == 0       // ... which is empty ...
      yield
        // Generate an option by updating the grid, replacing the stone at
        // `coord` with an empty space and the empty space at `coord + direction`
        // with a stone of the player's color.
        Position(grid Updated { coord => 0, coord + direction => us })
      end

    end

  end

  // Define the characters used to represent blank spaces, black stones,
  // and white stones when Clobber positions are created
  override def CharMap := ".xo";

  // Define the icons used to display blank spaces, black stones, and
  // white stones when Clobber positions are displayed
  override def Icons := [Icon.Blank, Icon.BlackStone, Icon.WhiteStone];

end
