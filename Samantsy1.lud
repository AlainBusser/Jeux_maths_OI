(define "SlideForwardToEmpty"
	(move Slide
		Forward
                (between 
                	if:
                		(is Empty (between)
                	)
                ) 
	)
)
(define "SamantsyPawn"
	(piece #1 Each
		(or {
			"SlideForwardToEmpty"
			("StepToEnemy" (directions {FR FL}))
			#2
			}
			#3
		)
	)
)

//------------------------------------------------------------------------------

(game "Samantsy" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        ("ChessKing" "King_noCross")
        (piece "Ferz_noCross"
                ("StepToNotFriend" Diagonal)
        )
        (piece "Elephant"
            (move Slide Diagonal 
                (between 
                	(exact 2)
                	if:
                		(is Empty (between)
                	)
                ) 
                (to 
                    if:("IsEnemyAt" (to))
                    (apply 
                        (remove (to))
                    ) 
                ) 
            )		
        )
        ("ChessKnight" "Knight")
        ("ChessRook" "Rook")
        ("SamantsyPawn" "Crab" ~ 
        	(then ("PromoteIfReach" (sites Mover "Promotion") "Ferz_noCross"))
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    
    (rules 
        (start { 
            (place "Crab1" (sites Row 1))
            (place "Crab2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        (play 
            (do (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
                (if
                        (not
                            (can
                                Move
                                (do
                                    (forEach Piece Next)
                                    ifAfterwards:(not
                                        (is Threatened (id "King_noCross" Next))
                                    )
                                )
                            )
                        )
                   	(result Mover Win)
                )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Game from Ikongo (Madagascar) reported in 1912")
        (rules "8x8 board. The pieces move as follows, with the number per player: 1 x hova (king): moves one space orthogonally or diagonally. 1 x anakova (counselor): 1 step diagonally. 2 x vorona (rook): Any number of spaces orthogonally. No castling. 2 x basy (elephant): Two steps diagonally, 
        jumping over an empty place. 2 x farasy (horse): Moves as a chess knight. 8 x zaza (pawn): slides forwards orthogonally; one space forward diagonally to capture. No en passant. Promoted to anakova when reaching the eighth rank. An opponent's piece is captured by moving a player's own piece onto a space occupied by the opponent's piece. When a hova can be captured on the next turn by an opponent's piece, it is in check. The hova must not be in check at the end of the player's turn. If this is not possible, it is checkmate and the opponent wins. Stalemate results in a win for that player causing it.
        ")
        (source "Ardant du Picq 1912: 267-268.")
        (version "1.4.0")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Alain Busser")
        (origin "This game was played in Madagascar, from around 1900 to 1940.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour Phase1 (colour 223 178 110))
        (board Colour InnerEdges (colour Brown))
        (board Colour OuterEdges (colour Black))
    })
    
)
